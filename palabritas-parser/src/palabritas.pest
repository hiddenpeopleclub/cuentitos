Char = { ASCII_ALPHANUMERIC | "." | "_" | "/" | "," | ";" | "'" | " " | "?" | "!" | "¿" | "¡"}
Integer = { "-"? ~ ASCII_DIGIT+ }
Float = { Integer ~ "." ~ ASCII_DIGIT* }
Percentage = { Integer ~ "%" }
Indentation = { "  " | "\t" }
String = { Char+ }
ComparisonOperator = { "!=" | "=" | "<=" | ">=" | "<" | ">" | "!" }

SnakeCase = { ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | "_" | ASCII_DIGIT)* }
Identifier = { (ASCII_ALPHA | "_" ) ~ (ASCII_ALPHANUMERIC | "_")* }
Value = { Identifier | Float | Percentage | Integer}
Condition = { Identifier ~ " "* ~ (ComparisonOperator ~ " "*)? ~ Value }

Requirement = { "req" ~ " "+ ~ Condition }
Frequency = { "freq" ~ " "+ ~ Condition ~ " "+ ~ ( Float | Integer ) }
Modifier = { "mod" ~ " "+ ~ Identifier ~ " "+ ~ Value}
Divert = { "->"  ~ " "* ~ Identifier ~ ("." ~ Identifier)? }

Command = { (Requirement | Frequency | Modifier | Divert) } 

Probability = { "(" ~ " "* ~ ( Percentage | Float | Integer ) ~ " "* ~ ")" ~ " "* }

Knot = {"===" ~ " "* ~ Identifier ~ " "* ~"==="}
Stitch = {"=" ~ " "* ~ Identifier}

Text = { Probability? ~ String }
Option = { "*" ~ " "* ~ Text }
NamedBucket = { "[" ~ " "* ~ Probability? ~ SnakeCase ~ " "* ~ "]"}

NewBlock = _{
    // The first line in the block
    PEEK_ALL ~ PUSH( Indentation+ ) ~ BlockContent ~
    // Subsequent lines in the block
    (PEEK_ALL ~ BlockContent)* ~
    // Remove the last layer of indentation from the stack when exiting the block
    DROP
}

BlockContent = {
    (Command | NamedBucket | Option | Knot | Stitch | Text)  ~  " "* ~ (NEWLINE | EOI) ~ NewBlock*
}

File = { SOI ~ (NEWLINE | BlockContent)* ~ EOI }