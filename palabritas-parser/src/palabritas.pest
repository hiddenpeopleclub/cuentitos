char = { ASCII_ALPHANUMERIC | "." | "_" | "/" | "," | ";" | "'" | " " | "?" | "!" | "¿" | "¡"}
integer = { "-"? ~ ASCII_DIGIT+ }
float = { integer ~ "." ~ ASCII_DIGIT* }
percentage = { integer ~ "%" }
indentation = { "  " | "\t" }
string = { char+ }
comparison_operator = { "!=" | "=" | "<=" | ">=" | "<" | ">" | "!" }

snake_case = { ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | "_" | ASCII_DIGIT)* }
identifier = { (ASCII_ALPHA | "_" ) ~ (ASCII_ALPHANUMERIC | "_")* }
value = { identifier | float | percentage | integer}
condition = { identifier ~ " "* ~ (comparison_operator ~ " "*)? ~ value }

requirement = { "req" ~ " "+ ~ condition }
frequency = { "freq" ~ " "+ ~ condition ~ " "+ ~ ( float | integer ) }
modifier = { "mod" ~ " "+ ~ identifier ~ " "+ ~ value}
divert = { "->"  ~ " "* ~ identifier ~ ("." ~ identifier)? }

command = { NEWLINE ~ indentation* ~ (requirement | frequency | modifier | divert) ~ " "*} 

probability = { "(" ~ " "* ~ ( percentage | float | integer ) ~ " "* ~ ")" ~ " "* }

knot = {"===" ~ " "* ~ identifier ~ " "* ~"===" ~ " "*}
stitch = {"=" ~ " "* ~ identifier ~ " "*}

text = { probability? ~ string }

option = { indentation* ~ "*" ~ " "* ~ text ~ command* }
text_shown = { indentation* ~ text ~ command* }
named_bucket = { indentation* ~ "[" ~ " "* ~ probability? ~ snake_case ~ " "* ~ "]" ~ " "* ~ command* }

file = { SOI ~ (( named_bucket | text_shown | option | indentation+ | knot | stitch)? ~ NEWLINE)* ~ (named_bucket | text_shown | option | indentation+ | knot | stitch)? ~ EOI }