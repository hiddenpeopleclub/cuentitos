Char = { ASCII_ALPHANUMERIC | "." | "_" | "/" | "," | ";" | "'" | " " | "?" | "!" | "¿" | "¡"}
Integer = { "-"? ~ ASCII_DIGIT+ }
Float = { Integer ~ "." ~ ASCII_DIGIT* }
Percentage = { Integer ~ "%" }
Indentation = { "  " }
String = @{ Char+ }
ComparisonOperator = { "!=" | "=" | "<=" | ">=" | "<" | ">" | "!" }

SnakeCase = { ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | "_" | ASCII_DIGIT)* }
Identifier = { (ASCII_ALPHA | "_" ) ~ (ASCII_ALPHANUMERIC | "_")* }
Value = { Identifier | Float | Percentage | Integer}
Condition = { Identifier ~ " "* ~ (ComparisonOperator ~ " "*)? ~ Value }

Requirement = { "req" ~ " "+ ~ Condition }
Frequency = { "freq" ~ " "+ ~ Condition ~ " "+ ~ ( Float | Integer ) }
Modifier = { "mod" ~ " "+ ~ Identifier ~ " "+ ~ Value}
Divert = { "->"  ~ " "* ~ Identifier ~ ("." ~ Identifier)? }
Function = {"`" ~ SnakeCase ~ (" " ~ Value)* ~ "`"}

Command = {NEWLINE ~ (Indentation | " ")* ~ (Requirement | Frequency | Modifier | Divert | Function) } 

Probability = { "(" ~ " "* ~ ( Percentage | Float | Integer ) ~ " "* ~ ")" ~ " "* }

Section = {"#" ~ " "* ~ Identifier ~ " "* ~ NEWLINE ~ ( NEWLINE | Block | Subsection )* }
Subsection = { "##" ~ " "* ~ Identifier ~ " "* ~ (NEWLINE | EOI) ~ NewBlock*}

Text = { Probability? ~ String }
Choice = { "*" ~ " "* ~ Probability? ~ String }
NamedBucket = { "[" ~ " "* ~ Probability? ~ SnakeCase ~ " "* ~ "]" }

NewBlock = {
    // The first line in the block
    PEEK_ALL ~ PUSH( Indentation+ ) ~ Block ~
    // Subsequent lines in the block
    (PEEK_ALL ~ Block)* ~
    // Remove the last layer of indentation from the stack when exiting the block
    DROP
}

Block = {
    (NamedBucket | Choice | Text)  ~  " "* ~ Command* ~ " "* ~ (NEWLINE | EOI) ~ NewBlock*
}

File = { SOI ~ (NEWLINE | Block | Section )* ~ EOI }