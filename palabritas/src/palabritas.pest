ReservedKeywords = { "req" | "freq" | "mod" | "->" | "`" | "*" | "#" | "##" | "//" }
Char = { (!NewLine ~ ANY)+ }
Integer = { "-"? ~ ASCII_DIGIT+ }
Float = { Integer ~ "." ~ ASCII_DIGIT* }
Percentage = { Integer ~ "%" }
Indentation = { "  " }
String = @{ Char+ }
ComparisonOperator = { "!=" | "=" | "<=" | ">=" | "<" | ">" | "!" }

SnakeCase = { ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | "_" | ASCII_DIGIT)* }
Identifier = { (ASCII_ALPHA | "_" ) ~ (ASCII_ALPHANUMERIC | "_")* }
Value = { Identifier | Float | Percentage | Integer}
Condition = { "!"? ~ Identifier ~ " "* ~ (ComparisonOperator ~ " "*)? ~ Value? }

Requirement = { "req" ~ " "+ ~ Condition }
Frequency = { "freq" ~ " "+ ~ Condition ~ " "+ ~ ( Float | Integer ) }
Modifier = { "mod" ~ " "+ ~ Identifier ~ " "+ ~ Value}
Divert = { "->"  ~ " "* ~ Identifier ~ ("." ~ Identifier)? }
Function = {"`" ~ SnakeCase ~ (" " ~ Value)* ~ "`"}

Command = {NewLine ~ (Indentation | " ")* ~ (Requirement | Frequency | Modifier | Divert | Function) } 

Probability = { "(" ~ " "* ~ ( Percentage | Float | Integer ) ~ " "* ~ ")" ~ " "* }

Section = {"#" ~ " "* ~ Identifier ~ " "* ~ Command* ~ ( NewLine | NewBlock | Subsection )* }
Subsection = { "##" ~ " "* ~ Identifier ~ " "* ~ (NewLine | EOI) ~ (NewBlock | NewLine)*}

Text = { !Indentation ~ !ReservedKeywords ~ Probability? ~ String }
Choice = { "*" ~ " "* ~ Probability? ~ String }
NamedBucket = { "[" ~ " "* ~ Probability? ~ SnakeCase ~ " "* ~ "]" }

NewBlock = {
    // The first line in the block
    PEEK_ALL ~ PUSH( Indentation+ ) ~ Block ~
    // Subsequent lines in the block
    (PEEK_ALL ~ Block)* ~
    // Remove the last layer of indentation from the stack when exiting the block
    DROP
}

Block = {
    (NamedBucket | Choice | Text)  ~  " "* ~ Command* ~ " "* ~ (NewLine | EOI) ~ NewBlock*
}

File = { SOI ~ (NewLine | Block | Section )* ~ EOI }

COMMENT = _{ "//" ~ (!(NewLine | EOI) ~ ANY)* ~ (NewLine | EOI) }
NewLine = _{ EmptyLine | NEWLINE }
EmptyLine = _{" "* ~ NEWLINE }